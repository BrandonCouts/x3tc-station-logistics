#name: plugin.LI.FDN.Reset
#lang: 44
#origin_mod: FDN
#source: mods/FDN/scripts/plugin.LI.FDN.Reset.xml

* Author: Logain Abler
* -----------------------------------------------------------------------------------
* this script resets all FDN locally stored values

* Expected values:
* - n/a

* Return values:
* - n/a

* Version: 1.0
* Date: 17/05/2012
* Tested: 17/05/2012
* -----------------------------------------------------------------------------------

* Fetch values saved in the AL Plugin global variable:
* DC
* factory array
* dock array
$al.Settings = get global variable: name='al.LI.FDN.event'
if $al.Settings
$dc = $al.Settings[2]
$factory.array = $al.Settings[3]
$dock.array = $al.Settings[4]
end
* -----------------------------------------------------------------------------------

$station = $dc
* clear the DC
gosub Clear.Local.Sub:
* -----------------------------------------------------------------------------------

* loop the factory array and clear
$sf = size of array $factory.array
while $sf > 0
dec $sf =
$station = $factory.array[$sf]
gosub Clear.Local.Sub:
end
* -----------------------------------------------------------------------------------

* loop the dock array and clear
$sd = size of array $dock.array
while $sd > 0
dec $sd =
$station = $dock.array[$sd]
gosub Clear.Local.Sub:
end
* -----------------------------------------------------------------------------------

* set AL Plugin global variable values
$al.Settings[2] = null
$factory.array = = array alloc: size=0
$al.Settings[3] = $factory.array
$dock.array = = array alloc: size=0
$al.Settings[4] = $dock.array
set global variable: name='al.LI.FDN.event' value=$al.Settings
* -----------------------------------------------------------------------------------
return null
* -----------------------------------------------------------------------------------

* this sub clears the locall stored values
Clear.Local.Sub:
$array = $station  ->  get local variables: regular expression='FDN*'
$s = size of array $array
while $s > 0
dec $s =
$var = $array[$s]
$station  ->  set local variable: name=$var value=null
end
endsub
* -----------------------------------------------------------------------------------
return null


