#name: plugin.LI.FDN.Menu.AP.DC.Details
#lang: 44
#origin_mod: FDN
#source: mods/FDN/scripts/plugin.LI.FDN.Menu.AP.DC.Details.xml

* Author: Logain Abler
* -----------------------------------------------------------------------------------
* This script builds the DC Details Menu and retuns the array back to the Main Menu
* Called by plugin.LI.FDN.Main.Menu

* Expected values:
* - menu - This is the menu array from the Main Menu
* - ware.show.array - This array hold the values of what Ware Type is displayed or hiden
* Ware.show.array can be null

* Return values:
* - return.array[update menu array][ware.show.array]

* Version: 1.0 - AP
* Date: 22/05/2012
* Tested: 22/05/2012
* -----------------------------------------------------------------------------------

* Fetch values saved in the AL Plugin global variable:
$al.Settings = get global variable: name='al.LI.FDN.event'
if $al.Settings
$PageID = $al.Settings[1]
$dc = $al.Settings[2]
end
* -----------------------------------------------------------------------------------

* The ID code of the DC is used as a reference in naming the global variable
$id = $dc  ->  get ID code
* -----------------------------------------------------------------------------------

* $txt = = read text: page=$PageID id=115
* add custom menu heading to array $menu: title=$txt
$txt = = read text: page=$PageID id=129
add custom menu info line to array $menu: text=$txt
add custom menu info line to array $menu: text=' '
* -----------------------------------------------------------------------------------

* Calls script to format name
$txt = [THIS] -> call script plugin.LI.FDN.Format.Name : object=$dc
$temp.array = = array alloc: size=4
$temp.array[0] = 1
$temp.array[1] = $txt
$temp.array[2] = -1
$txt = $dc  ->  get sector
$temp.array[3] = $txt
add custom menu info line to array $menu: text=$temp.array
* -----------------------------------------------------------------------------------

$pointer = = sprintf: pageid=$PageID textid=5001, $id, null, null, null, null
$array = get global variable: name=$pointer
add custom menu heading to array $menu: title=$array
* -----------------------------------------------------------------------------------

* all group options
$show.all = = create menu return value: command Open All Groups
$txt = = read text: page=$PageID id=198
add custom menu item to array $menu: text=$txt returnvalue=$show.all
$hide.all = = create menu return value: command Close All Groups
$txt = = read text: page=$PageID id=199
add custom menu item to array $menu: text=$txt returnvalue=$hide.all
add section to custom menu: $menu
* -----------------------------------------------------------------------------------

* This call the plugin.LI.FDN.Sort.Wares which returns a wares arrays sorted by type
* $sorted.ware.array[x] is the Main Type
* $sorted.ware.array[x][x] is the sorted Ware Array for the Main Type
$sorted.ware.array = [THIS] -> call script plugin.LI.FDN.Sort.Wares : station=$dc
$sm = size of array $sorted.ware.array
* -----------------------------------------------------------------------------------

* Create and size show type array
$show.type.array = = array alloc: size=$sm
* -----------------------------------------------------------------------------------

while $sm > 0
dec $sm =
* Fetch main.type and ware.array from sorted.ware.array
$main.type = $sorted.ware.array[$sm][0]
$ware.array = $sorted.ware.array[$sm][1]
* -----------------------------------------------------------------------------------

if not $show.type.array[$sm]
$show.type.array[$sm] = 1
end
$check = $show.type.array[$sm]
* -----------------------------------------------------------------------------------

* ware section
$sw = size of array $ware.array
* -----------------------------------------------------------------------------------

* only show arws if there are some to diplay for the main type
if $sw > 0
* -----------------------------------------------------------------------------------

* add grouping and ware header line
$txt = = read text: page=$PageID id=$main.type
add new grouping to menu: $menu, text=$txt, open=$check
gosub Get.Ware.Header.Sub:
* -----------------------------------------------------------------------------------

while $sw > 0
dec $sw =
$ware = $ware.array[$sw]

* Calls the global variable for the dynamic menu created by plugin.LI.FDN.Menu.DC.Details_D
* DC ID code and Ware are used to call the unique global variable
$pointer = = sprintf: pageid=$PageID textid=5000, $id, $ware, null, null, null
$summary = get global variable: name=$pointer
append $summary to array $menu
* -----------------------------------------------------------------------------------
end
add end grouping to menu: $menu
end
end
* -----------------------------------------------------------------------------------

* add ware
$txt = = read text: page=$PageID id=195
add custom menu heading to array $menu: title=$txt
$return.array = = array alloc: size=2
$return.array[0] = 'add.ware'
$return.array[1] = $dc
$txt = = read text: page=$PageID id=196
add custom menu item to array $menu: text=$txt returnvalue=$return.array
* -----------------------------------------------------------------------------------

* config menu
$txt = = read text: page=$PageID id=164
add custom menu heading to array $menu: title=$txt
$txt = = read text: page=$PageID id=165
add custom menu item to array $menu: text=$txt returnvalue='open.config'
* -----------------------------------------------------------------------------------

return $menu
* -----------------------------------------------------------------------------------

* This Sub is used to create the wares header by main type
Get.Ware.Header.Sub:
$temp.array = = array alloc: size=12
$temp.array[0] = 1
$txt = = read text: page=$PageID id=201
$temp.array[1] = $txt
$temp.array[2] = -470
$txt = = read text: page=$PageID id=142
$temp.array[3] = $txt
$temp.array[4] = -380
$txt = = read text: page=$PageID id=144
$temp.array[5] = $txt
$temp.array[6] = -290
$txt = = read text: page=$PageID id=143
$temp.array[7] = $txt
$temp.array[8] = -200
$txt = = read text: page=$PageID id=145
$temp.array[9] = $txt
$temp.array[10] = -1
$txt = = read text: page=$PageID id=140
$temp.array[11] = $txt
add non selectable menu item: $menu, text=$temp.array
endsub
* -----------------------------------------------------------------------------------
return null



