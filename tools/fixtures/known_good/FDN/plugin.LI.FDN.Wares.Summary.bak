* Author: Logain Abler
* -----------------------------------------------------------------------------------
* This formats the warea fro the DC and Dock menus

* Expected values:
* - station
* - ware
* list.flag - this tells the script that the return is for the dosk list, not details

* Returns values:
* - N/A

* To Do - N/A

* Version: 1.0
* Date: 23/05/2012
* Tested: 23/05/2012
* -----------------------------------------------------------------------------------

* Fetch values saved in the AL Plugin global variable:
* - DC
$al.Settings = get global variable: name='al.LI.FDN.event'
if $al.Settings
 $PageID = $al.Settings[1]
 $dc = $al.Settings[2]
end
* -----------------------------------------------------------------------------------

$settings.array = null
$price = $station-> get price of ware $ware
* -----------------------------------------------------------------------------------

if $station == $dc
 
 $temp.array = array alloc: size=12
 $temp.array[0] = 1
 $temp.array[2] = -470
 $temp.array[4] = -380
 $temp.array[6] = -290
 $temp.array[8] = -200
 $temp.array[10] = -1
 gosub DC.Summary.Sub:
 * -----------------------------------------------------------------------------------
 
else
 $temp.array = array alloc: size=10
 $temp.array[0] = 1
 $temp.array[2] = -380
 $temp.array[4] = -290
 $temp.array[6] = -200
 $temp.array[8] = -1
 gosub Dock.Summary.Sub:
 * -----------------------------------------------------------------------------------
end

$return.value = create new array, arguments='open.ware', $station, $ware, null, null
$format.array = create new array, arguments=$temp.array, $return.value, null, null, null
return $format.array
* -----------------------------------------------------------------------------------

* Ware summary for the dc
DC.Summary.Sub:
 $pointer = sprintf: pageid=$PageID textid=4000, $ware, null, null, null, null
 $settings.array = $station-> get local variable: name=$pointer
 if not $settings.array
  gosub Build.Ware.Array.Sub:
  $settings.array = $station-> get local variable: name=$pointer
 end
 * -----------------------------------------------------------------------------------
 
 * Allow remote == [1]
 $remote.cap = $settings.array[1]
 if $remote.cap > 0
  $txt1 = read text: page=$PageID id=151
 else
  $txt1 = read text: page=$PageID id=150
 end
 * -----------------------------------------------------------------------------------
 
 * Allow Buy = [3]
 $buy = $settings.array[3]
 if $buy == 1
  $txt2 = read text: page=$PageID id=153
 else
  $txt2 = read text: page=$PageID id=152
 end
 * -----------------------------------------------------------------------------------
 
 * Allow Sell = [5]
 $sell.cap = $settings.array[5]
 if $sell.cap >= 0
  $txt3 = read text: page=$PageID id=155
 else
  $txt3 = read text: page=$PageID id=154
 end
 * -----------------------------------------------------------------------------------
 
 $pre = sprintf: pageid=$PageID textid=9000, '- ', null, null, null, null
 $ware.txt = $pre + $txt1 + '|' + $txt2 + '|' + $txt3 + ' ~ ' + $ware
 $temp.array[1] = $ware.txt
 * -----------------------------------------------------------------------------------
 
 * Remote
 $remote.cap = $settings.array[1]
 if $remote.cap >= 0
  $remote.cap = convert number $remote.cap to string
  $remote.cap = sprintf: pageid=$PageID textid=9000, $remote.cap, null, null, null, null
  $temp.array[3] = $remote.cap
 else
  $temp.array[3] = ' - '
 end
 * -----------------------------------------------------------------------------------
 
 * but at
 $buy.at = $settings.array[4]
 if not $buy.at > 0
  $settings.array[4] = $price
  $station-> set local variable: name=$pointer value=$settings.array
 end
 $buy.at = convert number $buy.at to string
 if $buy == 1
  $buy.at = sprintf: pageid=$PageID textid=9000, $buy.at, null, null, null, null
  $temp.array[5] = $buy.at
 else
  $temp.array[5] = $buy.at
 end
 * -----------------------------------------------------------------------------------
 
 * sell cap
 if $sell.cap >= 0
  $txt = convert number $sell.cap to string
  $txt = sprintf: pageid=$PageID textid=9000, $txt, null, null, null, null
  $temp.array[7] = $txt
 else
  $temp.array[7] = ' - '
 end
 * -----------------------------------------------------------------------------------
 
 * sell at
 $sell.at = $settings.array[6]
 if not $sell.at > 0
  $settings.array[6] = $price
  $station-> set local variable: name=$pointer value=$settings.array
 end
 $sell.at = convert number $sell.at to string
 if $sell.cap >= 0
  $sell.at = sprintf: pageid=$PageID textid=9000, $sell.at, null, null, null, null
  $temp.array[9] = $sell.at
 else
  $temp.array[9] = $sell.at
 end
 * -----------------------------------------------------------------------------------
 
 * storage physical and virtual
 $amount = $station-> get amount of ware $ware in cargo bay
 $amount = convert number $amount to string
 $virtual = $settings.array[0]
 if not $virtual > 0
  $settings.array[0] = 0
  $virtual = $settings.array[0]
 end
 $virtual = convert number $virtual to string
 $virtual = sprintf: pageid=$PageID textid=9004, $virtual, null, null, null, null
 $storage = $amount + '/' + $virtual
 $temp.array[11] = $storage
endsub
* -----------------------------------------------------------------------------------

* Ware summary for the dc
Dock.Summary.Sub:
 $pointer = sprintf: pageid=$PageID textid=4000, $ware, null, null, null, null
 $settings.array = $station-> get local variable: name=$pointer
 if not $settings.array
  gosub Build.Ware.Array.Sub:
  $settings.array = $station-> get local variable: name=$pointer
 end
 * -----------------------------------------------------------------------------------
 
 * sell ware
 $sell.cap = $settings.array[5]
 if $sell.cap >= 0
  $txt1 = read text: page=$PageID id=155
 else
  $txt1 = read text: page=$PageID id=154
 end
 
 if $settings.array[7]
  $txt2 = read text: page=$PageID id=155
 else
  $txt2 = read text: page=$PageID id=154
 end
 * -----------------------------------------------------------------------------------
 
 $pre = sprintf: pageid=$PageID textid=9000, '- ', null, null, null, null
 $ware.txt = $pre + $txt1 + '|' + $txt2 + ' ~ ' + $ware
 $temp.array[1] = $ware.txt
 * -----------------------------------------------------------------------------------
 
 * maintain
 $maintain.stock = $settings.array[2]
 if $maintain.stock >= 0
  $maintain.stock = convert number $maintain.stock to string
  $maintain.stock = sprintf: pageid=$PageID textid=9000, $maintain.stock, null, null, null, null
  $temp.array[3] = $maintain.stock
 else
  $temp.array[3] = ' - '
 end
 * -----------------------------------------------------------------------------------
 
 * sell at
 $sell.at = $settings.array[6]
 if not $sell.at > 0
  $settings.array[6] = $price
  $station-> set local variable: name=$pointer value=$settings.array
 end
 $sell.at = convert number $sell.at to string
 if $sell.cap >= 0
  $sell.cap = convert number $sell.cap to string
  $sell.cap = sprintf: pageid=$PageID textid=9000, $sell.cap, null, null, null, null
  $temp.array[5] = $sell.cap
  $sell.at = sprintf: pageid=$PageID textid=9000, $sell.at, null, null, null, null
  $temp.array[7] = $sell.at
 else
  $temp.array[5] = ' - '
  $temp.array[7] = $sell.at
 end
 * -----------------------------------------------------------------------------------
 
 * storage physical and virtual
 $amount = $station-> get amount of ware $ware in cargo bay
 $amount = convert number $amount to string
 $virtual = $settings.array[0]
 if not $virtual > 0
  $settings.array[0] = 0
  $virtual = $settings.array[0]
 end
 $virtual = convert number $virtual to string
 $virtual = sprintf: pageid=$PageID textid=9004, $virtual, null, null, null, null
 $storage = $amount + '/' + $virtual
 $temp.array[9] = $storage
endsub
* -----------------------------------------------------------------------------------


* -----------------------------------------------------------------------------------
* add the virtula ref as none was found
Build.Ware.Array.Sub:
 $price = $station-> get price of ware $ware
 $settings.array = array alloc: size=12
 $settings.array[0] = 0
 $settings.array[1] = null
 $settings.array[2] = null
 $settings.array[3] = [FALSE]
 $settings.array[4] = $price
 $settings.array[5] = -1
 $settings.array[6] = $price
 $settings.array[7] = -1
 $settings.array[8] = null
 $settings.array[9] = null
 $settings.array[11] = null
 $station-> set local variable: name=$pointer value=$settings.array
 * if no physical wares found create and delete ware so it show no the dock
 $physical.check = $station-> get amount of ware $ware in cargo bay
 if not $physical.check > 0
  = $station-> add 1 units of $ware
  = $station-> add -1 units of $ware
 end
endsub
* -----------------------------------------------------------------------------------
return null


