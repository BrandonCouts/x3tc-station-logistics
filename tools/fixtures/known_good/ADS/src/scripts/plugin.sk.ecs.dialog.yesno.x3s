#name: plugin.sk.ecs.dialog.yesno
#lang: 44
#origin_mod: ADS
#source: mods/ADS/scripts/plugin.sk.ecs.dialog.yesno.xml

* ===========================================
* Extended Communication System 2.5
* ===========================================
* Call this script when you want to show a dialog window with 2 possible
* answers.
* ===========================================
* Script Settings:
* $ship : object sending the message (ship/station/roid/...)
* $sound : Sound ID to be player with 'send audio message..' command (can be null)
* $dialog.title : window's title
* $dialog.content : window's main text
* $dialog.yes : option 1 caption
* $dialog.no : option 2 caption
* ===========================================
* Script Returns:
* -1 : Player closed the window pressing ESC
* 1 : Player choosed the first answer
* 2 : Player choosed the second answer
* ===========================================

$page.id = 8511

if $ship -> is of class Station
$pilot.name = $ship -> get name
else
$pilot.name = $ship -> get pilot name
skip if $pilot.name
$pilot.name = $ship
end

skip if not $sound
$ship -> send audio message $sound to player

$menu =  create custom menu array

$st =  read text: page=$page.id id=224
add custom menu heading to array $menu: title=$st
add custom menu item to array $menu: text=$dialog.yes returnvalue=1
add custom menu item to array $menu: text=$dialog.no returnvalue=2

$st = sprintf: pageid=$page.id textid=225, $dialog.content, $pilot.name, null, null, null
add custom menu info line to array $menu: text=$st

$from = sprintf: pageid=$page.id textid=217, $ship, null, null, null, null
$menu.result =  open custom menu: title=$dialog.title description=$from option array=$menu


return $menu.result
