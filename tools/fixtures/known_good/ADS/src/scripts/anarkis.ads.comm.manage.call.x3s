#name: anarkis.ads.comm.manage.call
#lang: 44
#origin_mod: ADS
#source: mods/ADS/scripts/anarkis.ads.comm.manage.call.xml

$gl.setup = get global variable: name='anarkis.ads.setup'
$page.id = $gl.setup[0]
$cmd.id = 2010

*load text: id=$page.id

$m.filter = Carrier
$m.selected = null
$m.menumode = 'list'
$m.showcmd = [FALSE]
$m.showgridship = [FALSE]
$m.viewowned = [FALSE]

$is.open = get global variable: name='anarkis.ads.manager.open'
skip if not $is.open
return null
set global variable: name='anarkis.ads.manager.open' value=[TRUE]


$aim =  get player tracking aim
if $aim -> exists
$owner = $aim -> get owner race
$isbig = $aim -> is of class Big Ship
$isstation = $aim -> is of class Station
$compat = [THIS] -> call script anarkis.acc.is.compatible :  target=$aim
if $owner == Player

if $isbig == [TRUE] OR $isstation == [TRUE] OR $compat == [TRUE]
$m.selected = $aim
end
if $compat == [TRUE]
$m.filter = Carrier
else if $isbig == [TRUE]
$m.menumode = 'defense.grid'
end
if $isstation == [TRUE]
$m.filter = Station
end

end
end


$menu =  create custom menu array
$select =  array alloc: size=0
append $m.filter to array $select
append $m.selected to array $select
append $m.menumode to array $select
append $m.showcmd to array $select
append $m.showgridship to array $select
append $m.viewowned to array $select
set global variable: name='anarkis.ads.menu' value=$select

START $null -> call script anarkis.ads.comm.manage.menu :  menu=$menu

while $menu.result != -1

if $m.selected -> exists
$v1 = $m.selected -> get name
$v1 = [THIS] -> call script anarkis.lib.shortstring :  string=$v1  max=25
$v2 = $m.selected -> get sector
$v2 = sprintf: pageid=$page.id textid=1012, $v2, $v1, null, null, null
else
$v2 =  read text: page=$page.id id=100
end
$v1 =  read text: page=$page.id id=1040
= wait 100 ms
$menu.result =  open custom menu: title=$v1 description=$v2 option array=$menu

if  is datatyp[ $menu.result ] == DATATYP_OBJCLASS
$m.filter = $menu.result
$select[0] = $menu.result
$select[1] = null
$select[2] = 'list'
$m.menumode = 'list'
$m.selected = null
else if  is datatyp[ $menu.result ] == DATATYP_STRING

* - - Setup Carrier
if $menu.result == 'setup.object'
$m.selected -> start task 892 with script anarkis.acc.setup and prio 0: arg1=null arg2=null arg3=null arg4=null arg5=null
while $m.selected -> is task 892 in use
= wait 200 ms
end

else if $menu.result == 'gen.set'
= [THIS] -> call script anarkis.ads.setup.global :

* - - Defense Grid Mode
else if $menu.result == 'defense.grid'
$select[0] = null
$select[1] = null
$select[2] = 'grid.sector'
$m.menumode = 'grid.sector'
$m.selected = null

* - - Defense Grid Toggle
else if $menu.result == 'toggle.grid'
$m.selected = $select[1]
if $m.selected -> get local variable: name='anarkis.ads.grid'
$m.selected -> set local variable: name='anarkis.ads.grid' value=null
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
else
$m.selected -> set local variable: name='anarkis.ads.grid' value=[TRUE]
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
end

* - - Defense Grid Ship Manager
else if $menu.result == 'grid.ship' OR $menu.result == 'grid.sector'
$select[1] = null
$m.showgridship = $select[4]
$m.showgridship = ! $m.showgridship
$select[4] = $m.showgridship
$m.selected = null

else if $menu.result == 'grid.toggle'
if get global variable: name='anarkis.ads.grid.mode'
set global variable: name='anarkis.ads.grid.mode' value=null
= wait 1000 ms
else
set global variable: name='anarkis.ads.grid.mode' value=[TRUE]
START $null -> call script anarkis.ads.task.maingrid :  race=Player
end

* - - Toggle ADS
else if $menu.result == 'toggle.ads'
if [THIS] -> call script anarkis.acc.lib.isadsactive :  target=$m.selected
= [THIS] -> call script anarkis.ads.toggle.task :  target=$m.selected  activate=[FALSE]
else
= [THIS] -> call script anarkis.ads.toggle.task :  target=$m.selected  activate=[TRUE]
end

* - - Jump to Playership
else if $menu.result == 'jumptome'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script anarkis.lib.cmd.jumpnearobject :  destination=[PLAYERSHIP]

* - - Jump to Sector
else if $menu.result == 'jumpto'
$v1 =  read text: page=$page.id id=1026
$v1 = $m.selected -> get user input: type=Var/Jumpdrive Gate, title=$v1
if $v1 -> exists
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script !ship.cmd.jump.std :  target gate or sector=$v1  followers should jump too=[TRUE]
end

* - - Attack
else if $menu.result == 'attack'
$v1 =  read text: page=$page.id id=1027
$v1 = $m.selected -> get user input: type=Var/Ship/Station, title=$v1
if $v1 -> exists
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script anarkis.lib.cmd.attack :  the victim=$v1
end

* - - Dock Command
else if $menu.result == 'dockto'
$v1 =  read text: page=$page.id id=1026
$v1 = $m.selected -> get user input: type=Var/Station/Carrier to dock at, title=$v1
if $v1 -> exists
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script !ship.cmd.movestation.std :  object to land on=$v1
end

* - - Attack All
else if $menu.result == 'attackall'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script !ship.cmd.killenemies.std :

* - - Protect
else if $menu.result == 'protect'
$v1 = $m.selected -> get user input: type=Var/Ship/Station, title=$v1
if $v1 -> exists
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script anarkis.acc.cmd.protect :  protect target=$v1
end

* - - Standby
else if $menu.result == 'standby'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script !ship.cmd.stay.std :

* - - Patrol
else if $menu.result == 'patrol'
$v1 = $m.selected -> call script !ship.cmd.patrol.multi.pl :
$v3 = $m.selected -> get local variable: name='patrol.route'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script !ship.cmd.patrol.multi.std :  Patrol Route Array=$v3

* - - Dock All
else if $menu.result == 'dockall'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script anarkis.acc.cmd.dock.all.pl :

* - - Clear Sector
else if $menu.result == 'clearsector'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
START $m.selected -> call script anarkis.acc.wing.clearsector.pl :

* - - Attack Wing
else if $menu.result == 'wingattack'
$v1 =  read text: page=$page.id id=1027
$v1 = $m.selected -> get user input: type=Var/Ship/Station, title=$v1
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
= $m.selected -> call script anarkis.ads.wing.attack.pl :  target=$v1

* - - Defend Wing
else if $menu.result == 'wingdefend'
$v1 =  read text: page=$page.id id=1027
$v1 = $m.selected -> get user input: type=Var/Ship/Station, title=$v1
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
= $m.selected -> call script anarkis.ads.wing.defense.pl :  target=$v1

* - - Buy Wares
else if $menu.result == 'buywares'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
= $m.selected -> call script anarkis.acc.cmd.buywares.pl :

* - - Sell Wares
else if $menu.result == 'sellwares'
$m.selected -> set local variable: name='anarkis.ads.grid.busy' value=null
= $m.selected -> call script anarkis.acc.cmd.sellwares.pl :

* - - Show ECS News
else if $menu.result == 'alert'
= [THIS] -> call script plugin.sk.ecs.news.display :  Plugin ID='anarkis.acc.plugin'

* - - Toggle CMD Panel
else if $menu.result == 'cmdoff'
$select[3] = [FALSE]

else if $menu.result == 'cmdon'
$select[3] = [TRUE]

* - - Toggle View Ship List
else if $menu.result == 'viewships'
$v1 = $select[5]
$v1 = ! $v1
$select[5] = $v1

* - - Run un-installer
else if $menu.result == 'uninstall'
= [THIS] -> call script anarkis.acc.uninstall :
$m.menumode = 'quit'
$select[2] = 'quit'
set global variable: name='anarkis.ads.manager.open' value=null

return null

* - - Show on cursor
else if $menu.result == 'cursor'
set player tracking aim to $m.selected ->
$select[2] = 'quit'
set global variable: name='anarkis.ads.manager.open' value=null
return null
end

else if $menu.result == -1
if $m.selected != null
$select[1] = null
$select[5] = [FALSE]
$m.selected = null
$menu.result = 0
end
else if $menu.result -> exists
$select[1] = $menu.result
$m.selected = $menu.result
end

end
$select[2] = 'quit'
set global variable: name='anarkis.ads.manager.open' value=null


return null
