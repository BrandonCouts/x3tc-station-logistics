#name: anarkis.lib.armturret
#lang: 44
#origin_mod: ADS
#source: mods/ADS/scripts/anarkis.lib.armturret.xml

* ===========================================
* Custom Weapon Installation Script v1.1.1
* ===========================================
* weapon.select :
* - 0 : Fully Random Setup
* - 1 : Favor Shield Damage
* - 2 : Favor Hull Damage
* - 3 : Favor Weapon Range
* - 4 : Favor Weapon Speed
* - 5 : Overall Weapon Performances
* - 6 : Most Expensive Weapon
* ===========================================
* disable.ion = Disable ion disruptors
* disable.ammo = Disable all weapons using ammunitions
* ===========================================
* Fix 1 : No ammo weapon in turret 0 (avoid defenceless ships)
* Fix 2 : Default "attack enemies" turret settings
* Fix 3 : Dual weapon in slot 0
* Fix 4 : No turret script on OOS NPC ships
* Fix 4 : Replaced disable all ion by ion disruptor only
* Fix 5 : Reduced ion probability to 1/3
* ===========================================
* TODO : Better Missile turrets handling
* ===========================================

* Get all compatible laser types
$weap.array = [THIS] -> get compatible laser array: turret=$turret.nb
$turret.laser.cnt = [THIS] -> get max. number of lasers in turret $turret.nb
$weap.size =  size of array $weap.array
skip if $turret.laser.cnt
return [FALSE]

* Remove crappy and forbidden laser types
while $weap.size
*= wait randomly from 10 to 50 ms
dec $weap.size =
$test = $weap.array[$weap.size]
* - Remove useless laser types
if $test == Tractor Beam OR $test == Repair Laser OR $test == Mobile Drilling System OR $test == Impulse Ray Emitter
remove element from array $weap.array at index $weap.size
continue
end
* - Remove Ammo weapons
* - Fix 1 : No ammo weapon in turret 0 (avoid defenseless ships)
if $disable.ammo == [TRUE] OR $turret.nb == 0
$ammo.type =  get ammunition of laser $test
if $ammo.type
remove element from array $weap.array at index $weap.size
continue
end
end
* - Remove Ion weapons
* - Fix 5 : Reduced ion probability to 1/3
$third =  = random value from 0 to 3 - 1
if $disable.ion == [TRUE] OR ( $disable.ion == [FALSE] AND $third != 1 )
if $test == Ion Disruptor
remove element from array $weap.array at index $weap.size
continue
end
end
end

$weap.size =  size of array $weap.array
skip if $weap.size
return [FALSE]

* Random selection
if $weapon.select == 0
$weap.id =  = random value from 0 to $weap.size - 1
$weap.selected = $weap.array[$weap.id]
* Favor Shield Damage
else if $weapon.select == 1
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
dec $x =
$weap.selected = $weap.array[$x]
$dmg.tot =  get shield damage of laser $weap.selected
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
$weap.selected = $weap.array[$weap.id]
* Favor Hull Damage
else if $weapon.select == 2
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
dec $x =
$weap.selected = $weap.array[$x]
$dmg.tot =  get hull damage of laser $weap.selected
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
* Favor Weapon Range
else if $weapon.select == 3
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
dec $x =
$weap.selected = $weap.array[$x]
$dmg.tot =  get range of laser $weap.selected
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
* Favor Weapon Bullet Speed
else if $weapon.select == 4
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
*= wait randomly from 10 to 50 ms
dec $x =
$weap.selected = $weap.array[$x]
$dmg.tot =  get bullet speed of laser $weap.selected
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
* Overal Weapon Strength
else if $weapon.select == 5
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
*= wait randomly from 10 to 50 ms
dec $x =
$weap.selected = $weap.array[$x]
$w.shield =  get shield damage of laser $weap.selected
$w.hull =  get hull damage of laser $weap.selected
$w.range =  get range of laser $weap.selected
$w.speed =  get bullet speed of laser $weap.selected
$dmg.tot = 5 * $w.hull + $w.shield + 2 * $w.speed + 2 * $w.range
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
* Weapon Price
else if $weapon.select == 6
$x = $weap.size
$dmg.last = 0
$weap.id = 0
while $x
dec $x =
$weap.selected = $weap.array[$x]
$dmg.tot = get average price of ware $weap.selected
if $dmg.tot > $dmg.last
$weap.id = $x
$dmg.last = $dmg.tot
end
end
end

* Add and install

* Fix 3 : Dual weapon in slot 0
if $turret.laser.cnt >= 6 AND $turret.nb == 0
$fix = $turret.laser.cnt / 2
$weap.selected = $weap.array[$weap.id]
= [THIS] -> add $fix units of $weap.selected
$rnd =  = random value from 0 to $weap.size - 1
$fix.weapon = $weap.array[$rnd]
= [THIS] -> add $fix units of $fix.weapon
$x = $fix
while $x
dec $x =
[THIS] -> switch laser in turret $turret.nb gun $x to $weap.selected
end
$x = $fix
while $x
dec $x =
$rnd = $fix + $x
[THIS] -> switch laser in turret $turret.nb gun $rnd to $fix.weapon
end
else
$weap.selected = $weap.array[$weap.id]
= [THIS] -> add $turret.laser.cnt units of $weap.selected
$x = $turret.laser.cnt
while $x
dec $x =
[THIS] -> switch laser in turret $turret.nb gun $x to $weap.selected
end
end

* Check for ammo
$ammo.type =  get ammunition of laser $weap.selected
if $ammo.type
$test = 5 * $turret.laser.cnt
= [THIS] -> add $test units of $ammo.type
[THIS] -> set ammo resupply: ammo=$ammo.type amount=20
end

skip if not $turret.nb == 0
return $weap.selected

* Fix 4 : No turret script on OOS NPC ships
$pl.sector = [PLAYERSHIP] -> get sector
$owner = [THIS] -> get owner race
if $owner != Player AND [SECTOR] != $pl.sector
return $weap.selected
end

* Fix 2 : Default "attack enemies" turret settings
[THIS] -> start task $turret.nb with script !turret.killenemies.std and prio 0: arg1=$turret.nb arg2=null arg3=null arg4=null arg5=null


return $weap.selected
